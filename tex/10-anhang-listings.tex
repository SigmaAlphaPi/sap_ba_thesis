%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\newpage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\subsection*{Code-Listings}
\addcontentsline{toc}{subsection}{Code-Listings}

\begin{lstlisting}[style=asl, 
                   keywords={cellsize_in_meter,timestep_in_minutes,simulationtime_in_minutes,length_in_km,lanes}, 
                   keywords={[2]count,viewrange,speed,acceleration,deceleration}, 
                   keywords={[3]}, 
                   caption={Scenariovereinbarung},
                   label={lst:scenariofile}]      
# global section of configuration
main:

    # statistic result ("summary" = aggregated values, "descriptive" = aggregated & single values)
    statistic: "descriptive"

    # simulated time
    simulationtime_in_minutes: 60

    # prettyprint of the result json
    prettyprint: true

    # time in ms for printing a message to avoid CircleCI timeout (CircleCI time can be set to 480000)
    alive: 0

    # line breaker after each step
    linebreak: "----- step {0} ----------------"

    # runtime
    runtime:

        # type of the runtime, parameter is the thread number: ->synchronized<-, workstealing, fixedsize(T), cached, scheduled(T), single
        type: "synchronized"

        # number of threads of the runtime (fixedsize, scheduled)
        threads: 1

    # unit of the simulation
    unit:

        # cell size / size of a vehicle in meter
        cellsize_in_meter: 2.5

        # time of a single timestep in simulation
        # 0.1 -> 6 sec, 0.05 -> 3 sec, 0.025 -> 1.5 sec, 0.0125 -> 0.75 sec
        timestep_in_minutes: 0.025


# environment / street setting
environment:

    # number of lanes
    lanes: 1

    # length if the lane in kilometer
    length_in_km: 7.5


# agent vehicle configuration
agent:

    # global constants for all vehicles (constant name
    # must start with an upper-case letter)
    constant:
        AllowedSpeed: 100

    # all vehicle configuration, key is the filename of the filename
    # AgentSpeak(L++) script relative to configuration file
    source:
        vehicle.asl:

            # number of generated vehicles
            count: 34

            # show beliefs in each cycle
            showlog: false

            # view range in meters around a vehicle 
            # (should be greater than cell size)
            viewrange: 250

            # local constant values for this vehicle
            costant:
                LocalValue: "nothing"

            # speed range configuration for initialize and maximum
            # speed (maximum value will be set by random)
            speed:
                min: 80
                max: 130

            # acceleration configuration (the value is
            # calculated by random)
            acceleration:
                min: 3.5
                max: 7

            # decceleration configuration (the value is
            # calculated by random)
            deceleration:
                min: 8
                max: 10
\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\newpage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


\begin{lstlisting}[style=asl, 
                   keywords={+!cruise,+!accelerate,+!linger,+!decelerate,+!vehicle/collision}, 
                   keywords={[2]}, 
                   keywords={[3]}, 
                   caption={Agentenscript: NaSch-Modell, einspurig},
                   label={lst:nasch}]      
/*
 *  ASL file        NaSch model traffic, single lane
 */

!cruise.

// --- start all other plans ---
+!cruise <-
    !accelerate;
    !decelerate;
    !linger;
    !cruise
.

// --- acceleration ---
+!accelerate
    // --- accelerate only, if no traffic ahead ---
    // --- otherwise you have to brake against the acceleration ---
    // --- resulting in too long braking distances ---
    : 
        CurrentSpeed < AllowedSpeed
        && ~>>( view/vehicle( _, data( _, static( lane( FwdLane ), cell( FwdCell ), speed( FwdSpeed ), distance( FwdDist ), direction( FwdDir ) ) ) ),
                bool/equal( generic/type/tostring( FwdDir ), "forward[]" )
                && FwdDist < CurrentSpeed
                && FwdSpeed < CurrentSpeed
            )
    <-
        vehicle/accelerate(0.5);
        !accelerate
.

// --- lingering ---
+!linger <-
    L = math/statistic/randomsimple;
    L < 0.1;
    vehicle/decelerate(0.3)
.

// --- deceleration ---
+!decelerate 
/*
    // --- decelerate if max. allowed speed is reached ---
    // --- if commented out vehicle will cruise slightly over speed limit ---
    : CurrentSpeed > AllowedSpeed <-
        vehicle/decelerate(0.05);
        !decelerate
*/
    // --- decelerate if traffic is ahead ---
    // --- and own speed is higher than distance ---
    : >>( view/vehicle( _, data( _, static( lane( FwdLane ), cell( FwdCell ), speed( FwdSpeed ), distance( FwdDist ), direction( FwdDir ) ) ) ), 
            bool/equal( generic/type/tostring( FwdDir ), "forward[]" ) 
            && FwdDist < CurrentSpeed
        ) <-
        vehicle/decelerate(1);
        !decelerate
.

// --- collision ---
+!vehicle/collision <-
/*
    // --- brake as hard as possible ---
    vehicle/decelerate( 1 );
*/
    // --- stop immediately ---
    vehicle/stop;
.\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\newpage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


\begin{lstlisting}[style=asl, 
                   keywords={+!cruise,+!accelerate,+!linger,+!decelerate,+!vehicle/collision}, 
                   keywords={[2]}, 
                   keywords={[3]}, 
                   caption={Agentenscript: NaSch-Modell, mehrspurig},
                   label={lst:multi-lane}]      
/*
 *  ASL file        NaSch model traffic, multi lane
 */

!cruise.

// --- start all other plans ---
+!cruise <-
    !accelerate;
    !decelerate;
    !pullout;
    !pullin;
    !linger;
    !cruise
.

// --- NOCH NICHT VOLLSTAENDIG ---
\end{lstlisting}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\newpage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


\begin{lstlisting}[style=asl, 
                   keywords={}, 
                   keywords={[2]}, 
                   keywords={[3]}, 
                   caption={Agentenscript: Auszug Bedingungen pullout-Plan},
                   label={lst:pullout-cond}]      
/*
 *  ASL file        pullout conditions (separate for testing)
 */

+!pullouttest <-
    // --- TEST SETUP FOR THE CONDITIONS ---
    // --- each condition has an output line for control purposes ---

    Cond1 = CurrentLane < Lanes; 
    generic/print("vehicle is not on leftmost lane:", Cond1); 

    Cond2 = >>( view/vehicle( _, data( _, static( lane( FwdLane ), cell( FwdCell ), speed( FwdSpeed ), distance( FwdDist ), direction( FwdDir ) ) ) ),
                bool/equal( generic/type/tostring( FwdDir ), "forward[]" ) 
                && math/floor( FwdLane ) == CurrentLane
                && FwdDist < 175 
                ); 
    generic/print("traffic ahead in own lane:", Cond2); 

    Cond3 = ~>>( view/vehicle( _, data( _, static( lane( FwdLane2 ), cell( FwdCell2 ), speed( FwdSpeed2 ), distance( FwdDist2 ), direction( FwdDir2 ) ) ) ),
                bool/equal( generic/type/tostring( FwdDir2 ), "forward[]" ) 
                && math/floor( FwdLane2 ) == CurrentLane+1
                && FwdDist2 < FwdDist 
                ); 
    generic/print("lane to pull into is not worse:", Cond3); 

    Cond4 = ~>>( view/vehicle( _, data( _, static( lane( LeftLane ), cell( LeftCell ), speed( LeftSpeed ), distance( LeftDist ), direction( LeftDir ) ) ) ),
                bool/equal( generic/type/tostring( LeftDir ), "left[]" ) 
                && math/floor( LeftLane ) == CurrentLane+1
                );
    generic/print("no traffic directly to the left on lane to pull into:", Cond4);

    Cond5 = ~>>( view/vehicle( _, data( _, static( lane( BwdLane ), cell( BwdCell ), speed( BwdSpeed ), distance( BwdDist ), direction( BwdDir ) ) ) ),
                bool/equal( generic/type/tostring( BwdDir ), "backward[]" ) 
                && math/floor( BwdLane ) == CurrentLane+1
                && BwdDist < 100
                && BwdSpeed > CurrentSpeed 
                );
    generic/print("not hindering traffic behind in lane to pull into", Cond5);

    Combined = Cond1 && Cond2 && Cond3 && Cond4 && Cond5;
    generic/print("all combined", Combined)
.\end{lstlisting}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\newpage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\begin{lstlisting}[style=asl, 
                   keywords={}, 
                   keywords={[2]}, 
                   keywords={[3]}, 
                   caption={Agentenscript: Auszug Bedingungen pullin-Plan},
                   label={lst:pullin-cond}]      
/*
 *  ASL file        pullin conditions (separate for testing)
 */

+!pullintest <-
    // --- TEST SETUP FOR THE CONDITIONS ---
    // --- each condition has an output line for control purposes ---

    Cond1 = CurrentLane > 1;
    generic/print("vehicle is not on rightmost lane:", Cond1);

    Cond2 = ~>>( view/vehicle( _, data( _, static( lane( FwdLane ), cell( FwdCell ), speed( FwdSpeed ), distance( FwdDist ), direction( FwdDir ) ) ) ),
                bool/equal( generic/type/tostring( FwdDir ), "forward[]" ) 
                && math/floor( FwdLane ) == CurrentLane-1
                );
    generic/print("no other vehicle in forward view on lane to pull into", Cond2);

    Cond3 = ~>>( view/vehicle( _, data( _, static( lane( RightLane ), cell( RightCell ), speed( RightSpeed ), distance( RightDist ), direction( RightDir ) ) ) ),
                bool/equal( generic/type/tostring( RightDir ), "right[]" ) 
                && math/floor( RightLane ) == CurrentLane-1
                );
    generic/print("no other vehicle directly to the right on lane to pull into", Cond3);

    Cond4 = ~>>( view/vehicle( _, data( _, static( lane( BwdLane ), cell( BwdCell ), speed( BwdSpeed ), distance( BwdDist ), direction( BwdDir ) ) ) ),
                bool/equal( generic/type/tostring( BwdDir ), "backward[]" ) 
                && math/floor( BwdLane ) == CurrentLane-1
                && BwdDist < 125
//                && BwdSpeed > CurrentSpeed 
                );
    generic/print("not hindering traffic behind on lane to pull into", Cond4);

    Combined = Cond1 && Cond2 && Cond3 && Cond4;
    generic/print("all combined", Combined)
.\end{lstlisting}
